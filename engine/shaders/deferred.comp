#version 460
#include "lib/common.glsl"
#include "lib/constants.glsl"
#include "lib/gbuffer.glsl"
#include "lib/lighting.glsl"
#include "lib/shadow.glsl"
#include "lib/ubo.glsl"

layout (local_size_x = 8, local_size_y = 8) in;

DECLARE_UBO(0, 0);
layout (binding = 1, std430) readonly buffer Lights {
    uint g_light_count;
    PointLight g_lights[];
};
layout (binding = 2, rgba8) writeonly uniform image2D g_output_image;

layout (set = 1, binding = 0) uniform texture2D g_albedo_image;
layout (set = 1, binding = 1) uniform texture2D g_normal_image;
layout (set = 1, binding = 2) uniform texture2D g_depth_image;
layout (set = 1, binding = 4) uniform sampler2DArrayShadow g_shadow_map;
layout (set = 1, binding = 5) readonly buffer LightVisibilities {
    LightVisibility g_light_visibilities[];
};

const vec3 k_clear_colour = vec3(0.47f, 0.5f, 0.67f);

void main() {
    vec4 albedo = texelFetch(g_albedo_image, ivec2(gl_GlobalInvocationID.xy), 0);
    if (albedo.a == 0.0f) {
        imageStore(g_output_image, ivec2(gl_GlobalInvocationID.xy), linear_to_srgb(vec4(k_clear_colour, 1.0f)));
        return;
    }
    ivec2 tile_id = ivec2(gl_GlobalInvocationID.xy / k_tile_size);
    uint tile_index = tile_id.y * k_row_tile_count + tile_id.x;
    vec3 normal = decode_normal(texelFetch(g_normal_image, ivec2(gl_GlobalInvocationID.xy), 0).rg);

    float depth = texelFetch(g_depth_image, ivec2(gl_GlobalInvocationID.xy), 0).r;
    vec4 world_position = vec4(gl_GlobalInvocationID.xy / vec2(k_viewport_width, k_viewport_height) * 2.0f - 1.0f, depth, 1.0f);
    world_position = g_inv_proj_view * world_position;
    world_position /= world_position.w;

    vec3 illuminance = albedo.rgb * 0.02f;
    vec3 view = normalize(g_view_position - world_position.xyz);

    // Sun.
    const vec3 sun_direction = vec3(0.6f, 0.6f, -0.6f);
#if 0
    float sun_shadow = compute_shadow(world_position, g_view, g_shadow_map, g_shadow_info);
#else
    float sun_shadow = 1.0f;
#endif
    illuminance += compute_light(albedo.rgb, vec3(1.0f), sun_direction, normal, view) * sun_shadow;

    // Point lights.
    for (uint i = 0; i < g_light_visibilities[tile_index].count; i++) {
        PointLight light = g_lights[g_light_visibilities[tile_index].indices[i]];
        vec3 direction = normalize(light.position - world_position.xyz);
        float dist = distance(light.position, world_position.xyz);
        float attenuation = clamp(1.0f - dist * dist / (light.radius * light.radius), 0.0f, 1.0f);
        illuminance += compute_light(albedo.rgb, light.colour, direction, normal, view) * attenuation;
    }

    // Final output.
    imageStore(g_output_image, ivec2(gl_GlobalInvocationID.xy), linear_to_srgb(vec4(illuminance, 1.0f)));
}
